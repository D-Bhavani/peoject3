group 'tech.toparvion'
version '1.6-beta'

apply plugin: 'java'
apply plugin: 'jacoco'

sourceCompatibility = JavaVersion.VERSION_1_6   // not a typo,
targetCompatibility = JavaVersion.VERSION_1_6   // but a way to support old apps
compileTestJava.options.encoding = 'UTF-8'
archivesBaseName = "jmint"

repositories {
    mavenCentral()
}

configurations {
    provided
    implementation.extendsFrom provided
}

dependencies {
    implementation group: 'org.javassist', name: 'javassist', version: '3.27.0-GA'
    implementation group: 'org.antlr', name: 'antlr4-runtime', version: '4.5.3'
    
    testImplementation group: 'junit', name: 'junit', version: '4.13'
    testImplementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'
    testRuntimeClasspath group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.7'
}

jar {
    manifest {
        attributes("Implementation-Title": "jMint",
                "Implementation-Version": archiveVersion,
                "Implementation-Vendor": "Toparvion",
                "Premain-Class": "tech.toparvion.jmint.JMintAgent",
                "Main-Class": "tech.toparvion.jmint.DropletChecker")
    }
    from {
        (configurations.runtimeClasspath - configurations.provided)
                .collect({ it.isDirectory() ? it : zipTree(it) })
    }
    exclude ('**/*.g4')  // there is no need to export grammar files as they are already exported via generated parsers
}

task runPainterWithDebug(type: JavaExec, dependsOn: jar) {
    classpath = sourceSets.test.runtimeClasspath
                 /* + project.files('src/test/java/sampleapp/standalone/painter/slf4j-simple-1.7.30.jar')*/
    main 'sampleapp.standalone.painter.Painter'
    jvmArgs = ['-Dfile.encoding=UTF8',
               '-javaagent:build/libs/jmint-'+version+'.jar=' +
                       'src/test/java/sampleapp/standalone/painter/composite-droplet.zip'
    ].toList()
    debug = true            // Gradle default debug port is 5005
}

test {
    useJUnit()
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}